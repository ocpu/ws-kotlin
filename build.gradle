plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'org.jetbrains.dokka' version '0.9.16'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
    id 'signing'
}

group 'io.opencubes'
version '0.0.2'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.61"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.1.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'A WebSocket implementation'
                url = 'https://github.com/ocpu/ws-kotlin#readme'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'ocpu'
                        name = 'Martin HÃ¶vre'
                        email = 'martin.hovre@opencubes.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ocpu/ws-kotlin.git'
                    developerConnection = 'scm:git:ssh://github.com:ocpu/ws-kotlin.git'
                    url = 'http://github.com/ocpu/ws-kotlin/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenKotlin
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenKotlin']
    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = 'https://github.com/ocpu/ws-kotlin'
        issueTrackerUrl = 'https://github.com/ocpu/ws-kotlin/issues'
        vcsUrl = 'https://github.com/ocpu/ws-kotlin.git'
        licenses = ['MIT']
//        publish = true
        githubRepo = 'ocpu/ws-kotlin'
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = property("signing.password")
            }
        }
    }
}
